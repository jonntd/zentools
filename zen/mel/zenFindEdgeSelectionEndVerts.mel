//find the end verts of an edge loop
global proc string[] zenFindEdgeSelectionEndVerts(string $edgeLoop[])
{
		$edgeLoop=`ls -fl $edgeLoop`;
		
	string	$ends[];
	string	$testEdges[];
	string	$testVerts[];
	string	$intersector=`stringArrayIntersector`;
	
		for($i=0;$i<size($edgeLoop);$i++)
		{
			$testVerts=`polyListComponentConversion -tv $edgeLoop[$i]`;
			$testVerts=`ls -fl $testVerts`;
			$testEdges=`polyListComponentConversion -te $testVerts[0]`;
			$testEdges=`ls -fl $testEdges`;
			
			stringArrayIntersector -e -r $intersector;
			stringArrayIntersector -e -i $testEdges $intersector;
			stringArrayIntersector -e -i $edgeLoop $intersector;
			$testEdges=`stringArrayIntersector -q $intersector`;
						
			if(size($testEdges)==1)
			{
				$ends[size($ends)]=$testVerts[0];
			}
			else
			{
				$testEdges=`polyListComponentConversion -te $testVerts[1]`;
				$testEdges=`ls -fl $testEdges`;
				
				stringArrayIntersector -e -r $intersector;
				stringArrayIntersector -e -i $testEdges $intersector;
				stringArrayIntersector -e -i $edgeLoop $intersector;
				$testEdges=`stringArrayIntersector -q $intersector`;
								
				if(size($testEdges)==1)		
					$ends[size($ends)]=$testVerts[1];
			}
		}
		
		deleteUI $intersector;
	return $ends;
}