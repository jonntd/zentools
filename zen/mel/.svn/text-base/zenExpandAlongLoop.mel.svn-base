//expand edge loops by $distance
global proc string[] zenExpandAlongLoop(int $distance, string $edges[])
{
		$edges=stringArrayRemove({""," "},`ls -fl $edges`);
	
	string	$obj=zenReturnFirstStringItem( stringToStringArray( $edges[0],"." ) );
	
	string	$edgesExpanded[];
	string	$edgesLoops[];
	
	int	$err=false;
	
		$edgesExpanded=$edges;
		
		for($i=0; $i<$distance; $i++ )
		{
			$edgesExpanded=ls
			(
				"-fl",
				polyListComponentConversion
				(
					"-te",
					polyListComponentConversion
					(
						"-tv",
						$edgesExpanded
					)
				)
			);
		}
		for( $e in $edges )
		{
			$err=catch
			(
				$edgesLoops=stringArrayCatenate
				(
					$edgesLoops,
					stringArrayCatenate
					(
						zenCompFromID
						(
							$obj,
							"e",
							polySelect( "-ns", "-eb", zenCompID($e), $obj )
						) ,
						zenCompFromID
						(
							$obj,
							"e",
							polySelect( "-ns", "-el", zenCompID($e), $obj )
						) 
					)
				)
			);
			
			if($err) warning("\nProblem with zenExpandAlongLoop:"+$e+"\n");
		}
		
		//$edgesLoops=stringArrayRemoveDuplicates($edgesLoops);
		
	return	( zenStringArrayIntersect( $edgesExpanded, $edgesLoops ) );
}
