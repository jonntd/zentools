global proc zenSaveOptions()
{
	string	$vars[]=`env`;
	string	$zenIntVars[];
	string	$zenStringVars[];
	string	$tempArray[];
	string	$prefix;

		for($v in $vars)
		{
			$prefix=zenReturnFirstStringItem(`stringToStringArray $v "_"`);

			if($prefix=="$zenOptions")
				$zenIntVars=`stringArrayCatenate $zenIntVars {$v}`;

			if($prefix=="$zenOptionString")
				$zenStringVars=`stringArrayCatenate $zenStringVars {$v}`;
		}

	string	$userPrefDirectory=`internalVar -upd`;
	string	$userOptions=$userPrefDirectory+"zenTools_userOptions.mel";

	string	$writeCmd;

	global	string	$zenCurrentOption;

		for($z in $zenIntVars)
		{
			eval("$zenCurrentOption=string("+$z+")");
			$writeCmd=$writeCmd+($z+"="+$zenCurrentOption+";\n");
		}

		for($z in $zenStringVars)
		{
			eval("$zenCurrentOption="+$z);
			$writeCmd=$writeCmd+($z+"=\""+$zenCurrentOption+"\";\n");
		}

		$writeCmd=$writeCmd+"global proc zenTools_userOptions(){}\n";

	int	$fileID=`fopen $userOptions "w"`;

		fprint $fileID $writeCmd;

		fclose 	$fileID;
}